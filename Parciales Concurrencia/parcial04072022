programa parcial04072022
procesos
  proceso juntarFlores (ES f:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      f:=f+1
  fin
  proceso completarLado (ES f:numero)
  comenzar
    repetir 5
      juntarFlores(f)
      mover
    juntarFlores(f)
    derecha
  fin
areas
  Ar1: AreaP(1,1,6,6)
  Ar2: AreaP(7,1,12,6)
  Ar3: AreaP(13,1,18,6)
  AreaJefe: AreaP(20,1,20,1)
  {Podr√≠an entrar solo 2 robots}
  punto: AreaPC(10,10,10,10)
robots  
  robot trabajador
  variables
    f:numero
    id:numero
    ok:boolean
    ganador:numero
    av:numero
    ca:numero
  comenzar
    av:= PosAv
    ca:= PosCa
    RecibirMensaje(id,jefe)
    repetir 4
      completarLado(f)
      EnviarMensaje(V,jefe)
      RecibirMensaje(ok,jefe)
    EnviarMensaje(id,jefe)
    EnviarMensaje(f,jefe)
    RecibirMensaje(ganador,jefe)
    si (ganador <> id)
      repetir f
        BloquearEsquina(10,10)
        Pos(10,10)
        depositarFlor
        pos(av,ca)
        LiberarEsquina(10,10)
  fin
  robot coordinador
  variables
    listo:boolean
    f:numero
    id:numero
    max:numero
    maxID:numero
  comenzar
    max := 0
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    repetir 4
      repetir 3
        RecibirMensaje(listo,*)
      EnviarMensaje(V,r1)
      EnviarMensaje(V,r2)
      EnviarMensaje(V,r3)
    repetir 3
      RecibirMensaje(id,*)
      si (id = 1)
        RecibirMensaje(f,r1)
      sino
        si (id = 2)
          RecibirMensaje(f,r2)
        sino
          RecibirMensaje(f,r3)
      si (max < f)
        max := f
        maxID := id
    Informar('El_ganador_fue',maxID)
    Informar('El_ganador_junto_',max)
    EnviarMensaje(maxID,r1)
    EnviarMensaje(maxID,r2)
    EnviarMensaje(maxID,r3)
  fin
variables
  r1:trabajador
  r2:trabajador
  r3:trabajador
  jefe:coordinador
comenzar
  AsignarArea(r1,Ar1)
  AsignarArea(r2,Ar2)
  AsignarArea(r3,Ar3)
  AsignarArea(jefe,AreaJefe)
  AsignarArea(r1,punto)
  AsignarArea(r2,punto)
  AsignarArea(r3,punto)
  Iniciar(r1,1,1)
  Iniciar(r2,7,1)
  Iniciar(r3,13,1)
  Iniciar(jefe,20,1)
fin